// set up a simple neutral simulation
initialize() {
	setSeed(SEED); //SEED is input
	
	initializeMutationRate(1.5e-8);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	
	// m2 mutation type: adaptive (Set dominance coeff to 0.5)
	initializeMutationType("m2", 0.5, "f", SELCOEFF);
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 80 kb with uniform recombination
	initializeGenomicElement(g1, 1, 80000);
	initializeRecombinationRate(1e-8);
}

// create ancestral population of 10,000 individuals and burnin for 100,000 gen
1 {
	sim.addSubpop("p1", 10000);
}

100000{
	//decrease population to 2,000
	p1.setSubpopulationSize(2000);
}

100500{
	//increase population to 20,000
	p1.setSubpopulationSize(20000);
}

102900 late(){
	//selection
	target = sample(p1.genomes, 200); //initial frequency is 0.01
	target.addNewDrawnMutation(m2, 40000);
	
}

103500{
	//sample at present day
	p1Individuals = sample(p1.individuals, NINDIV); //NINDIV=64 for 128 haplotypes
	p1Individuals.genomes.outputMS(OUTPUT); //OUTPUT
}

