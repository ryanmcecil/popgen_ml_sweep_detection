// set up a simple neutral simulation
initialize() {
	initializeMutationRate(1e-8);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	
	// m2 mutation type: sweep
	initializeMutationType("m2", 0.5, "f", 0.0);
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 kb with uniform recombination
	initializeGenomicElement(g1, 0, 100000);
	initializeRecombinationRate(1e-8);
}

// create a population of 10000 individuals
1 {
	defineConstant("simID", getSeed());
	sim.addSubpop("p1", 10000);
	// set fitness of beneficial mutation to 0.005
	defineConstant("fit", 1.0+0.005);
}

// allow burn-in for 10,000 generations
10000 {
		// split off reference population
		sim.addSubpopSplit("p2", 10000, p1);
}

// let populations evolve separately for 1000 generations
11000 late(){
	//save the state of the simulation
	sim.outputFull("binary_sc005_middle/tmp_slim_"+simID+".txt");
	//introduce the adaptive mutation at position 50000
	target = sample(p1.genomes, 1);
	target.addNewDrawnMutation(m2, 50000);

}

//change selection coefficient to fit, let adaptation run for 700 generations
11000:11700 fitness(m2, p1){
	return fit;
}

//
11000:11700 late(){
	mut = sim.mutationsOfType(m2);
	if (size(mut)==1)
	{
		if (sim.mutationFrequencies(NULL, mut) > 0.2)
		{
			cat(simID+": ESTABLISHED\n");
			sim.deregisterScriptBlock(self);
		}
	}
	else
	{
	cat(simID+" LOST -- RESTARTING\n");
	//go back to generation 11000
	sim.readFromPopulationFile("binary_sc005_middle/tmp_slim_"+simID+".txt");
	//start a new run
	setSeed(rdunif(1, 0, asInteger(2^32)-1));
	//reintroduce sweep mutation
	target = sample(p1.genomes, 1);
	target.addNewDrawnMutation(m2,50000); 
	}
}

// at present generation (11700) -- output sample of 100 individuals from each population
11700 late(){
	p1Individuals = sample(p1.individuals, 100);
	p2Individuals = sample(p2.individuals, 100);
	p1Individuals.genomes.outputMS("binary_sc005_middle/p1_"+simID+"_MS.txt");
	vec = c(p1Individuals, p2Individuals);
	vec.genomes.outputMS("binary_sc005_middle/p1p2_"+simID+"_MS.txt");
}
